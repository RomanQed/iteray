import java.text.SimpleDateFormat

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    compileJava.options.encoding('UTF-8')
    withJavadocJar()
    withSourcesJar()
}

group = 'com.github.romanqed'
archivesBaseName = 'jiter-array'
version = System.getenv('JITER_VERSION') ?: '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    // Tests
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.10.0'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.10.0'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        var date = new Date()
        var javaVersion = System.getProperty("java.version")
        var vmVendor = System.getProperty("java.vm.vendor")
        var vmVersion = System.getProperty("java.vm.version")

        attributes(
                'Created-By': String.format("%s (%s %s)", javaVersion, vmVendor, vmVersion),
                'Gradle-Version': "Gradle $gradle.gradleVersion",
                'Build-Date': new SimpleDateFormat("yyyy-MM-dd").format(date),
                'Build-Time': new SimpleDateFormat("HH:mm:ss.SSSZ").format(date),
                'Built-By': 'Roman Bakaldin',
                'Bundle-Name': 'Java Array Iterable',
                'Bundle-Version': archiveVersion,
                'Bundle-SymbolicName': archivesBaseName,
                'Implementation-Title': archivesBaseName,
                'Implementation-Vendor': 'github.com/amayaframework',
                'Implementation-Version': archiveVersion,
                'Specification-Title': archivesBaseName,
                'Specification-Vendor': 'github.com/amayaframework',
                'Specification-Version': archiveVersion
        )
    }
}

signing {
    sign publishing.publications
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            // Specify artifacts
            artifactId = archivesBaseName
            groupId = group
            version = version
            from components.java
            // Configure pom
            pom {
                name.set(archivesBaseName)
                description.set('Lightweight utility for efficient, immutable iteration over Java arrays with slicing support.')
                url.set('https://github.com/RomanQed/jiter')

                organization {
                    name.set('com.github.romanqed')
                    url.set('https://github.com/RomanQed')
                }

                issueManagement {
                    system.set('GitHub')
                    url.set('https://github.com/RomanQed/jiter/issues')
                }

                licenses {
                    license {
                        name.set('Apache License 2.0')
                        url.set('https://github.com/RomanQed/jiter/blob/main/LICENSE')
                    }
                }

                scm {
                    url.set('https://github.com/RomanQed/jiter')
                    connection.set('scm:https://github.com/RomanQed/jiter.git')
                    developerConnection.set('scm:https://github.com/RomanQed/jiter.git')
                }

                developers {
                    developer {
                        id.set('RomanQed')
                        name.set('Roman Bakaldin')
                        email.set('gbakaldin@gmail.com')
                    }
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = centralUsername
            password = centralPassword
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
}
